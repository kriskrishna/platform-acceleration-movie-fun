import org.flywaydb.gradle.FlywayExtension
import org.flywaydb.gradle.task.FlywayMigrateTask

plugins {
    id "org.flywaydb.flyway" version "4.0.3"
}

apply from: "../../java.gradle"
apply from: "../db.gradle"

dependencies {
    compile "mysql:mysql-connector-java:$mysqlVersion"
}

flyway {
    url = "jdbc:mysql://localhost:3306/plays?useSSL=false"
    user = "root"
    outOfOrder = false
}

def tunnelProcess

task openTunnel {
    doLast {
        println 'Opening Tunnel'
        Thread.start {
            tunnelProcess = "cf ssh -N -L 63306:${getMysqlHost("play-service")}:3306 play-service".execute()
        }

        sleep 5_000L
    }
}

task closeTunnel {
    doLast {
        println 'Closing Tunnel'
        tunnelProcess.destroy()
    }
}

task cfMigrate(type: FlywayMigrateTask, dependsOn: openTunnel) {
    if (System.env.CF_MIGRATE) {
        extension = setupProdFlywayExtension(new FlywayExtension(), "play-service")
    }
}

cfMigrate.finalizedBy closeTunnel
